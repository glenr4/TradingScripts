// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© glenrutherford4

//@version=5
indicator("Engulfing Reversal", overlay = true, max_boxes_count = 500)

bullishColour = input.color(color.rgb(76, 175, 80), title="Bullish Colour", group= "Appearance")
bearishColour = input.color(color.rgb(255, 82, 82), title="Bullish Colour", group= "Appearance")

isBullish(i) =>
    close[i] > open[i]
isBearish(i) => 
    close[i] < open[i]

isEngulfing = high > high[1] and low < low[1]
isBullishEngulfing = isBullish(0) and isEngulfing
isBearishEngulfing = isBearish(0) and isEngulfing

plotshape(isBullishEngulfing, style=shape.triangleup, location=location.belowbar, color=bullishColour, size=size.tiny)
plotshape(isBearishEngulfing, style=shape.triangledown, location=location.abovebar, color=bearishColour, size=size.tiny)

// Large wick
enableLargeWickHighlight = input.bool(1, title="Enable Large Wick Highlight", group= "Large Wick")
avgPeriods = input.int(20, title="Average Periods", group= "Large Wick", minval = 0)
avgBodySizeMultiplier = input.float(1.2, title="Average Body Size Multiplier", group= "Large Wick", step=0.1, minval = 0)
minWickProportion = input.float(0.6, title="Minimum Wick Proportion", group= "Large Wick", step=0.1, minval = 0)

bullishHighlightColour = color.new(bullishColour, 50)
bearishHighlightColour = color.new(bearishColour, 50)

avgRange = ta.sma(high - low, avgPeriods)

isLargeRange(i) => 
    (high[i] - low[i]) > avgRange[i] * avgBodySizeMultiplier

fullRange = high - low
upperWickSize = high - math.max(open, close)
lowerWickSize = math.min(open, close) - low

isLargeBullishWick = lowerWickSize >= fullRange * minWickProportion
isLargeBearishWick = upperWickSize >= fullRange * minWickProportion

if(enableLargeWickHighlight and isBullishEngulfing and isLargeBullishWick and isLargeRange(0))
    box.new(bar_index - 1, high, bar_index + 1, low, bgcolor=bullishHighlightColour, border_color=bullishHighlightColour)

if(enableLargeWickHighlight and isBearishEngulfing and isLargeBearishWick and isLargeRange(0))
    box.new(bar_index - 1, high, bar_index + 1, low, bgcolor=bearishHighlightColour, border_color=bearishHighlightColour)
