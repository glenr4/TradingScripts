// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© glenrutherford4

// TODO: allow box to extend further than the most recent bar
// TODO: add signal for reversal FVG with twice body size

//@version=5
indicator("Fair Value Gap", overlay = true, max_boxes_count = 500, max_labels_count = 500)

showFVGs = input.bool(true, "Show Fair Value Gaps")
boxColour = input.color(color.new(#faf603, 70), "FVG Box Colour")
showOrderBlocks = input.bool(false, "Show Order Blocks")
obBoxColour = input.color(color.new(#b8b8b8, 90), "Order Block Box Colour")

// Triangle signal settings
showTriangles = input.bool(true, "Show Triangle Signals", group="Triangle Signals")
minFVGCount = input.int(3, "Minimum FVG Count", minval=2, maxval=10, group="Triangle Signals")
maxCandles = input.int(10, "Maximum Candles Lookback", minval=2, maxval=50, group="Triangle Signals")

boxLength = 3

type fvg
    box fvgBox
    float limit
    bool isBullish

type fvgOccurrence
    int barIndex
    bool isBullish

var fvg[] fvgs = array.new<fvg>(0)
var fvg[] obs = array.new<fvg>(0)   // Order Blocks
var fvgOccurrence[] fvgHistory = array.new<fvgOccurrence>(0)  // Track FVG occurrences for signals

isBullish(i) =>
    close[i] > open[i]
isBearish(i) => 
    close[i] < open[i]

isOverLapPriorBar(int middleCandleIndex) =>
    fvgSize = 0.0
    if middleCandleIndex >= 1
        previousCandleIndex = middleCandleIndex + 1
        nextCandleIndex = middleCandleIndex - 1

        isBullishMiddleCandle = isBullish(middleCandleIndex)

        // If there is overlap of previous and next candles then there is no gap to highlight
        isOverlapPrevNext = isBullishMiddleCandle ? low[nextCandleIndex] <= high[previousCandleIndex] : high[nextCandleIndex] >= low[previousCandleIndex]

        // To have the textbook version of FVG, this logic should be enalbed
        // But if the FVG is so strong that is actually gaps, then want to highlight that as well
        isTopBodyOverlapped = true  // isBullishMiddleCandle ? close[middleCandleIndex] >= low[nextCandleIndex] : open[middleCandleIndex] >= low[previousCandleIndex]
        isBottomBodyOverlapped = true //isBullishMiddleCandle ? open[middleCandleIndex] <= high[previousCandleIndex] : close[middleCandleIndex] <= high[nextCandleIndex]

        if not isOverlapPrevNext and isTopBodyOverlapped and isBottomBodyOverlapped
            if isBullishMiddleCandle
                if showFVGs
                    array.push(fvgs, fvg.new(box.new(bar_index - middleCandleIndex, low[nextCandleIndex], bar_index + boxLength, high[previousCandleIndex], bgcolor=boxColour, border_color=boxColour, border_width = 1), low[nextCandleIndex], true))
                if showOrderBlocks
                    array.push(obs, fvg.new(box.new(bar_index - previousCandleIndex, high[previousCandleIndex], bar_index + boxLength, low[previousCandleIndex], bgcolor=obBoxColour, border_color=obBoxColour, border_width = 1), high[previousCandleIndex], true))
                fvgSize := math.abs(high[previousCandleIndex] - low[nextCandleIndex])
                // Track FVG occurrence
                array.push(fvgHistory, fvgOccurrence.new(bar_index, true))
            else
                if showFVGs
                    array.push(fvgs, fvg.new(box.new(bar_index - middleCandleIndex, low[previousCandleIndex], bar_index + boxLength, high[nextCandleIndex], bgcolor=boxColour, border_color=boxColour, border_width = 1), high[nextCandleIndex], false))
                if showOrderBlocks
                    array.push(obs, fvg.new(box.new(bar_index - previousCandleIndex, high[previousCandleIndex], bar_index + boxLength, low[previousCandleIndex], bgcolor=obBoxColour, border_color=obBoxColour, border_width = 1), low[previousCandleIndex], false))
                fvgSize := math.abs(low[previousCandleIndex] - high[nextCandleIndex])
                // Track FVG occurrence
                array.push(fvgHistory, fvgOccurrence.new(bar_index, false))
    fvgSize // return value


//////////////////////////////
maxLength = 400

adjustBox(_array) =>
    int _boxCount = array.size(_array)

    if _boxCount > 0
        for _i = _boxCount - 1 to 0
            fvg  _fvg = array.get(_array, _i)

            box.set_right(_fvg.fvgBox, bar_index) // extend right

            bool  _fvgMitigated = (_fvg.isBullish ? low <= _fvg.limit : high >= _fvg.limit) and bar_index != box.get_left(_fvg.fvgBox)+1 // ignore until after FVG
            if _fvgMitigated or bar_index - box.get_left(_fvg.fvgBox) > maxLength
                array.remove(_array, _i)

            _fvgMitigated // return
    else
        false
////////////////////////////

fvgSize = isOverLapPriorBar(1) 

fvgMitigated = false
obMitigated = false

if showFVGs
    fvgMitigated := adjustBox(fvgs)

if showOrderBlocks
    obMitigated := adjustBox(obs)

// Check for multiple FVGs within lookback period
checkMultipleFVGs() =>
    bullishCount = 0
    bearishCount = 0

    // Remove old FVG occurrences outside the lookback window
    if array.size(fvgHistory) > 0
        for i = array.size(fvgHistory) - 1 to 0
            fvgOccurrence occ = array.get(fvgHistory, i)
            if bar_index - occ.barIndex > maxCandles
                array.remove(fvgHistory, i)

    // Count bullish and bearish FVGs within lookback
    if array.size(fvgHistory) > 0
        for i = 0 to array.size(fvgHistory) - 1
            fvgOccurrence occ = array.get(fvgHistory, i)
            if occ.isBullish
                bullishCount += 1
            else
                bearishCount += 1

    [bullishCount, bearishCount]

[bullishCount, bearishCount] = checkMultipleFVGs()

// Display triangle signals
bullishSignal = showTriangles and bullishCount >= minFVGCount
bearishSignal = showTriangles and bearishCount >= minFVGCount

plotshape(bullishSignal, style=shape.triangleup, location=location.belowbar, color=color.green, size=size.small, title="Bullish FVG Signal")
plotshape(bearishSignal, style=shape.triangledown, location=location.abovebar, color=color.red, size=size.small, title="Bearish FVG Signal")

// Signals
alertcondition(fvgMitigated,"FVG Mitigated","FVG Mitigated")
alertcondition(obMitigated,"OB Mitigated","OB Mitigated")
alertcondition(fvgSize > 0,"FVG Formed","FVG Formed")
alertcondition(bullishSignal, "Bullish FVG Cluster", "Multiple Bullish FVGs Detected")
alertcondition(bearishSignal, "Bearish FVG Cluster", "Multiple Bearish FVGs Detected")
