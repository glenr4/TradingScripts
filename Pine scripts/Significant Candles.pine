// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© glenrutherford4

//@version=5
indicator("Significant Candles", overlay = true, max_boxes_count = 500)

avgPeriods = input.int(100, "Average periods", group = "Common")
lineExtensionLength = input.int(50, title="Line extension length", group= "Common")
avgVolumeMultiplier = input.float(2.5, "Average volume multiplier", group = "Common", step = 0.1)

showShavedOpenCandles = input.bool(0, "Show shaved open candles", group = "Shaved Open Candles")
avgRangeMultiplier = input.float(1.0, "Average range multiplier", group = "Shaved Open Candles", step = 0.1)
shavedOpenWickTolerance = input.float(0.05, "Shaved open wick tolerance", group = "Shaved Open Candles", step = 0.01)
shavedOpenOppositeWickTolerance = input.float(0.35, "Shaved open opposite wick tolerance", group = "Shaved Open Candles", step = 0.01)

highlightHighWaves = input.bool(0, title="Highlight High Waves", group= "High Wave")
highWaveWickTolerance = input.float(0.6, title="Equal Wick Tolerance", group= "High Wave", step=0.1)
highWaveLargeRange = input.float(1.4, title="Large Range Multiplier", group= "High Wave", step=0.1)
maxBodyProportion = input.float(0.3, title="Maximum body proportion", group= "High Wave", step=0.1)

highlightSmallBodies = input.bool(1, title="Highlight small bodies", group= "Small Body")
avgSmallBodyPeriods = input.int(100, "Average periods", group = "Small Body")
maxSmallBodyProportion = input.float(0.5, title="Maximum body relative to average", group= "Small Body", step=0.1)

highlightSmallRanges = input.bool(1, title="Highlight small ranges", group= "Small Body")


isBullish(offset) =>
    close[offset] > open[offset]
isBearish(i) => 
    close[i] < open[i]

hasSmallBody(int offset) =>
    fullRange = high[offset] - low[offset]
    body = math.abs(open[offset] - close[offset])

    body < fullRange * maxBodyProportion    // return value

// Shaved open candles
shavedOpenBullishColour = color.green
shavedOpenBearishColour = color.red

upperWickRange = isBullish(0) ? high - close : high - open
lowerWickRange = isBullish(0) ? open - low : close - low
candleRange = high - low
avgRange = ta.sma(candleRange, avgPeriods)
isLargeRange = candleRange >= avgRange * avgRangeMultiplier

avgVolume = ta.sma(volume, avgPeriods)
isHighVolume = volume >= avgVolume * avgVolumeMultiplier and volume > 0

isShavedOpenBullishCandle = isBullish(0) and lowerWickRange/candleRange <= shavedOpenWickTolerance and isLargeRange
isStrongShavedOpenBullishCandle = isShavedOpenBullishCandle and upperWickRange/candleRange <= shavedOpenOppositeWickTolerance // and isHighVolume
isShavedOpenBearishCandle = isBearish(0) and upperWickRange/candleRange <= shavedOpenWickTolerance and isLargeRange
isStrongShavedOpenBearishCandle = isShavedOpenBearishCandle and lowerWickRange/candleRange <= shavedOpenOppositeWickTolerance // and isHighVolume

plotshape(isStrongShavedOpenBullishCandle and showShavedOpenCandles, style=shape.xcross, location=location.belowbar, color=color.yellow, size=size.tiny)
plotshape(isStrongShavedOpenBearishCandle and showShavedOpenCandles, style=shape.xcross, location=location.abovebar, color=color.yellow, size=size.tiny)

if(isStrongShavedOpenBullishCandle and showShavedOpenCandles)
    line.new(x1=bar_index, y1=low, x2=bar_index + lineExtensionLength, y2=low, color=color.yellow, width=1)
if(isStrongShavedOpenBearishCandle and showShavedOpenCandles)
    line.new(x1=bar_index, y1=high, x2=bar_index + lineExtensionLength, y2=high, color=color.yellow, width=1)

// High volume
barcolor(isHighVolume and isBullish(0) ? color.green : na)
barcolor(isHighVolume and isBearish(0) ? color.red : na)

// Highwave
highWaveColour = color.rgb(12, 216, 243, 50)

topWickSize = high - math.max(open, close)
bottomWickSize = math.min(open, close) - low
areEqualWicks = math.abs(topWickSize - bottomWickSize)/topWickSize < highWaveWickTolerance
largeRange = high - low >= avgRange * highWaveLargeRange

isHighWave = hasSmallBody(0) and areEqualWicks and largeRange

if(isHighWave and highlightHighWaves)
    box.new(bar_index-1, high, bar_index+1, low, bgcolor=highWaveColour, border_color=highWaveColour)
    line.new(x1=bar_index, y1=low, x2=bar_index + lineExtensionLength, y2=low, color=highWaveColour, width=1)
    line.new(x1=bar_index, y1=high, x2=bar_index + lineExtensionLength, y2=high, color=highWaveColour, width=1)


// Small body
smallBodyColour = color.rgb(94, 236, 120, 50)

body = math.abs(close - open)
avgBody = ta.sma(body, avgSmallBodyPeriods)
isSmallBody = body < avgBody * maxSmallBodyProportion

if(isSmallBody and highlightSmallBodies)
    box.new(bar_index-1, high, bar_index+1, low, bgcolor=smallBodyColour, border_color=smallBodyColour)

// Small range
smallRangeColour = color.rgb(94, 167, 236, 50)

rangeSize = high - low
avgSmallRange = ta.sma(rangeSize, avgSmallBodyPeriods)
isSmallRange = rangeSize < avgSmallRange * maxSmallBodyProportion

if(isSmallRange and highlightSmallRanges)
    box.new(bar_index-1, high, bar_index+1, low, bgcolor=smallRangeColour, border_color=smallRangeColour)
